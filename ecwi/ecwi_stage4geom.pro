;
; Copyright (c) 2013, California Institute of Technology. All rights
;	reserved.
;+
; NAME:
;	ECWI_STAGE4GEOM
;
; PURPOSE:
;	This procedure takes the data from basic CCD reduction through the
;	geometric correction, which includes solving for wavelength and 
;	spatial geometries.
;
; CATEGORY:
;	Data reduction for the EMCCD Cosmic Web Imager (ECWI).
;
; CALLING SEQUENCE:
;	ECWI_STAGE4GEOM, Pparfname, Linkfname
;
; OPTIONAL INPUTS:
;	Pparfname - input ppar filename generated by ECWI_STAGE2_PREP
;			defaults to './redux/ecwi.ppar'
;	Linkfname - input link filename generated by ECWI_PREP
;			defaults to './redux/ecwi.link'
;
; KEYWORDS:
;	SELECT	- set this keyword to select a specific image to process
;	PROC_IMGNUMS - set to the specific image numbers you want to process
;	PROC_CBARNUMS - set to the corresponding master dark image numbers
;	PROC_ARCNUMS - set to the corresponding master dark image numbers
;	NOTE: PROC_IMGNUMS and PROC_CBARNUMS and PROC_ARCNUMS must have the 
;		same number of items
;	VERBOSE	- set to verbosity level to override value in ppar file
;	DISPLAY - set to display level to override value in ppar file
;
; OUTPUTS:
;	None
;
; SIDE EFFECTS:
;	Outputs processed files in output directory specified by the
;	ECWI_PPAR struct read in from Pparfname.
;
; PROCEDURE:
;	Reads Pparfname to derive input/output directories and reads the
;	corresponding '*.link' file in output directory to derive the list
;	of input files and their associated geometric calibration files.
;
; EXAMPLE:
;	Perform stage4geom reductions on the images in 'night1/redux' directory:
;
;	ECWI_STAGE4GEOM,'night1/redux/ecwi.ppar'
;
; MODIFICATION HISTORY:
;	Written by:	Don Neill (neill@caltech.edu)
;	2013-AUG-01	Initial version
;	2014-JAN-30	Handles failure to trace cbars
;	2014-APR-03	Use master ppar and link files
;	2014-MAY-13	Include calibration image numbers in headers
;	2014-SEP-29	Added infrastructure to handle selected processing
;       2015-APR-25     Added CWI flexure hooks (MM)
;-
pro ecwi_stage4geom,ppfname,linkfname,help=help,select=select, $
	proc_imgnums=proc_imgnums, proc_cbarnums=proc_cbarnums, $
	proc_arcnums=proc_arcnums, $
	verbose=verbose, display=display
	;
	; setup
	pre = 'ECWI_STAGE4GEOM'
	startime=systime(1)
	q = ''	; for queries
	;
	; help request
	if keyword_set(help) then begin
		print,pre+': Info - Usage: '+pre+', Ppar_filespec, Link_filespec'
		print,pre+': Info - default filespecs usually work (i.e., leave them off)'
		return
	endif
	;
	; get ppar struct
	ppar = ecwi_read_ppar(ppfname)
	;
	; verify ppar
	if ecwi_verify_ppar(ppar,/init) ne 0 then begin
		print,pre+': Error - pipeline parameter file not initialized: ',ppfname
		return
	endif
	;
	; directories
	if ecwi_verify_dirs(ppar,rawdir,reddir,cdir,ddir) ne 0 then begin
		ecwi_print_info,ppar,pre,'Directory error, returning',/error
		return
	endif
	;
	; check keyword overrides
	if n_elements(verbose) eq 1 then $
		ppar.verbose = verbose
	if n_elements(display) eq 1 then $
		ppar.display = display
	;
	; specific images requested?
	if keyword_set(proc_imgnums) then begin
		nproc = n_elements(proc_imgnums)
		if n_elements(proc_cbarnums) ne nproc or $
		   n_elements(proc_arcnums) ne nproc then begin
			ecwi_print_info,ppar,pre,'Number of cbars and arcs must equal number of images',/error
			return
		endif
		imgnum = proc_imgnums
		cnums = proc_cbarnums
		anums = proc_arcnums
	;
	; if not use link file
	endif else begin
		;
		; read link file
		ecwi_read_links,ppar,linkfname,imgnum,cbar=cnums,arc=anums,count=nproc,select=select
		if imgnum[0] lt 0 then begin
			ecwi_print_info,ppar,pre,'reading link file',/error
			return
		endif
	endelse
	;
	; log file
	lgfil = reddir + 'ecwi_stage4geom.log'
	filestamp,lgfil,/arch
	openw,ll,lgfil,/get_lun
	ppar.loglun = ll
	printf,ll,'Log file for run of '+pre+' on '+systime(0)
	printf,ll,'DRP Ver: '+ecwi_drp_version()
	printf,ll,'Raw dir: '+rawdir
	printf,ll,'Reduced dir: '+reddir
	printf,ll,'Calib dir: '+cdir
	printf,ll,'Data dir: '+ddir
	printf,ll,'Filespec: '+ppar.filespec
	printf,ll,'Ppar file: '+ppar.ppfname
	if keyword_set(proc_imgnums) then begin
		printf,ll,'Processing images: ',imgnum
		printf,ll,'Using these cbars: ',cnums
		printf,ll,'Using these arcs : ',anums
	endif else $
		printf,ll,'Master link file: '+linkfname
	if ppar.saveintims eq 1 then $
		printf,ll,'Saving intermediate images'
	if ppar.clobber then $
		printf,ll,'Clobbering existing images'
	printf,ll,'Verbosity level   : ',ppar.verbose
	printf,ll,'Plot display level: ',ppar.display
	if ppar.saveplots eq 1 then $
		printf,ll,'Saving plots'
	;
	; gather configuration data on each observation in reddir
	ecwi_print_info,ppar,pre,'Number of input images',nproc
	;
	; loop over images
	for i=0,nproc-1 do begin
		;
		; image to process
		;
		; check for flat fielded image first
		obfil = ecwi_get_imname(ppar,imgnum[i],'_intf',/reduced)
		;
		; if not check for dark subtracted image
		if not file_test(obfil) then $
			obfil = ecwi_get_imname(ppar,imgnum[i],'_intd',/reduced)
		;
		; if not just get stage1 output image
		if not file_test(obfil) then $
			obfil = ecwi_get_imname(ppar,imgnum[i],'_int',/reduced)
		;
		; check if input file exists
		if file_test(obfil) then begin
			;
			; read configuration
			ecfg = ecwi_read_cfg(obfil)
			;
                        ; CWI FLEX ADDITION +++
                         if (ppar.biasskip1) then begin
                        ; check if there is a flexpar file load if it exists
                            flexparfil = ecwi_get_imname(ppar,imgnum[i],'_flexpar',/reduced) ;
                            if file_test(flexparfil) then begin
                               message,"Loaded: "+flexparfil,/info
                               flexpar = mrdfits(flexparfil,1) 
                               if flexpar.refmask then flexparmask = mrdfits(flexparfil, 2)
                            endif else begin
                               flexpar = 0
                               message,"Flexpar file missing!",/info
                            endelse 
                         endif  ; biasskip1  (/CWI)
                        ; CWI FLEX ADDITION ---
                        ;
			; final output file
			ofil = ecwi_get_imname(ppar,imgnum[i],'_icube',/reduced)
			;
			; get image type
			ecfg.imgtype = strtrim(ecfg.imgtype,2)
			;
			; check if output file exists already
			if ppar.clobber eq 1 or not file_test(ofil) then begin
				;
				; print image summary
				ecwi_print_cfgs,ecfg,imsum,/silent
				if strlen(imsum) gt 0 then begin
					for k=0,1 do junk = gettok(imsum,' ')
					imsum = string(i+1,'/',nproc,format='(i3,a1,i3)')+' '+imsum
				endif
				print,""
				print,imsum
				printf,ll,""
				printf,ll,imsum
				flush,ll
				;
				; do we have the geom links?
				do_geom = (1 eq 0)
				if cnums[i] ge 0 and anums[i] ge 0 then begin
					;
					; get filenames
					cbf = ecwi_get_imname(ppar,cnums[i],/nodir)
					arf = ecwi_get_imname(ppar,anums[i],/nodir)
					;
					; get corresponding egeom file
					gfile = cdir + strmid(cbf,0,strpos(cbf,'.fit'))+'_geom.save'
					;
					; if it exists restore it
					if file_test(gfile,/read) then begin
						restore,gfile
						do_geom = (egeom.status eq 0)
						;
						; log it
						ecwi_print_info,ppar,pre,'Using geometry from',gfile,format='(a,a)'
					;
					; if not, derive it
					endif else begin
						;
						; get reduced images (assume flat fielded images first)
						cbf = ecwi_get_imname(ppar,cnums[i],'_intf',/reduced)
						arf = ecwi_get_imname(ppar,anums[i],'_intf',/reduced)
						;
						; check for dark subtracted images next
						if not file_test(cbf) or not file_test(arf) then begin
							cbf = ecwi_get_imname(ppar,cnums[i],'_intd',/reduced)
							arf = ecwi_get_imname(ppar,anums[i],'_intd',/reduced)
						endif
						;
						; check for stage1 output images last
						if not file_test(cbf) or not file_test(arf) then begin
							cbf = ecwi_get_imname(ppar,cnums[i],'_int',/reduced)
							arf = ecwi_get_imname(ppar,anums[i],'_int',/reduced)
						endif
						;
						; log
						ecwi_print_info,ppar,pre,'Generating geometry solution'
						;
						; read configs
						ccfg = ecwi_read_cfg(cbf)
						acfg = ecwi_read_cfg(arf)
						;
						; create a new Egeom
						egeom = {ecwi_geom}
						egeom = struct_init(egeom)
						egeom.initialized = 1
						;
						; populate it with goodness
						ecwi_set_geom,egeom,ccfg,ppar
						egeom.cbarsfname = cbf
						egeom.cbarsimgnum = ccfg.imgnum
						egeom.arcfname = arf
						egeom.arcimgnum = acfg.imgnum
						;
						; read in cbars image
						cbars = mrdfits(cbf,0,chdr,/fscale,/silent)
						;
						; trace the bars
						ecwi_trace_cbars,cbars,egeom,ppar,status=stat
						;
						; check status, if < 0 don't proceed
						if stat ge 0 then begin
							;
							; log
							ecwi_print_info,ppar,pre,'traced continuum bars in cbars image',cbf,format='(a,a)'
							;
							; read in arcs
							arc = mrdfits(arf,0,ahdr,/fscale,/silent)
							;
							; extract along bars
							ecwi_extract_arcs,arc,egeom,spec,ppar
							;
							; log
							ecwi_print_info,ppar,pre,'extracted arc spectra from arc image',arf,format='(a,a)'
							;
							; do the solution
							ecwi_solve_geom,spec,egeom,ppar
							;
							; log bad solution
							if egeom.status ne 0 then $
								ecwi_print_info,ppar,pre,'bad geometry solution',/error
						endif else $
							ecwi_print_info,ppar,pre,'unable to trace cont bars',/error
						;
						; write out result
						ecwi_write_geom,ppar,egeom
					endelse
					;
					; is our geometry good?
					if egeom.status eq 0 then begin
						;
						; read in, update header, apply geometry, write out
						;
						; object image
						img = mrdfits(obfil,0,hdr,/fscale,/silent)
						;
						sxaddpar,hdr, 'COMMENT','  '+pre+' '+systime(0)
                                                ;
                                                ; CWI FLEX ADDITION/CHANGES +++
                                                ;
                                                if ppar.biasskip1 then begin
                                                ; Inject flexure stuff
                                                   cwi_flex_inject_one,ppar,egeom, flexpar, flexparmask
            
                                                   ecwi_apply_geom,img,hdr,egeom,ppar,cube,chdr,flex=flexpar
                                                endif else $; biasskip1 (/cwi)
                                                   ecwi_apply_geom,img,hdr,egeom,ppar,cube,chdr                               
						;
                                                ; CWI FLEX ADDITION/CHANGES ---
						;
						; write out intensity cube
						ofil = ecwi_get_imname(ppar,imgnum[i],'_icube',/nodir)
						ecwi_write_image,cube,chdr,ofil,ppar
						;
						; variance image
						vfil = repstr(obfil,'_int','_var')
						if file_test(vfil,/read) then begin
							var = mrdfits(vfil,0,varhdr,/fscale,/silent)
							;
							sxaddpar,varhdr,'COMMENT','  '+pre+' '+systime(0)
                                ;
                                ; CWI FLEX ADDITIONS + CHANGES +++
 if ppar.biasskip1 then $
                                                           ecwi_apply_geom,var,varhdr,egeom,ppar,vcub,vchdr,flex=flexpar else $
                                                           ecwi_apply_geom,var,varhdr,egeom,ppar,vcub,vchdr
                                                        ; CWI FLEX ADDITIONS + CHANGES ---
							;
							; write out variance cube
							ofil = ecwi_get_imname(ppar,imgnum[i],'_vcube',/nodir)
							ecwi_write_image,vcub,vchdr,ofil,ppar
						endif else $
							ecwi_print_info,ppar,pre,'no variance image found',/warning
						;
						; mask image
						mfil = repstr(obfil,'_int','_msk')
						if file_test(mfil,/read) then begin
							msk = float(mrdfits(mfil,0,mskhdr,/silent))
							;
                                                        sxaddpar,mskhdr,'COMMENT','  '+pre+' '+systime(0)
                                ; CWI FLEX ADDITIONS/CHANGES +++
                                                        ;
                                                        if ppar.biasskip1 then $
                                                           ecwi_apply_geom,msk,mskhdr,egeom,ppar,mcub,mchdr,flex=flexpar else $
                                                              ecwi_apply_geom,msk,mskhdr,egeom,ppar,mcub,mchdr   
                                ; CWI FLEX ADDITIONS/CHANGES ---
							;
							; write out mask cube
							ofil = ecwi_get_imname(ppar,imgnum[i],'_mcube',/nodir)
							ecwi_write_image,mcub,mchdr,ofil,ppar
						endif else $
							ecwi_print_info,ppar,pre,'no mask image found',/warning
						;
						; check for nod-and-shuffle sky images
						sfil = ecwi_get_imname(ppar,imgnum[i],'_sky',/reduced)
						if file_test(sfil,/read) then begin
							sky = mrdfits(sfil,0,skyhdr,/fscale,/silent)
							;
							sxaddpar,skyhdr,'COMMENT','  '+pre+' '+systime(0)
                                ;
                                ; CWI FLEX ADDITIONS/CHANGES +++
                                                        if ppar.biasskip1 then $
                                                           ecwi_apply_geom,sky,skyhdr,egeom,ppar,scub,schdr,flex=flexpar else $
                                                              ecwi_apply_geom,sky,skyhdr,egeom,ppar,scub,schdr
						
                                ; CWI FLEX ADDITIONS/CHANGES ---
							;
							; write out sky cube
							ofil = ecwi_get_imname(ppar,imgnum[i],'_scube',/nodir)
							ecwi_write_image,scub,schdr,ofil,ppar
						endif
						;
						; check for nod-and-shuffle obj images
						nfil = ecwi_get_imname(ppar,imgnum[i],'_obj',/reduced)
						if file_test(nfil,/read) then begin
							obj = mrdfits(nfil,0,objhdr,/fscale,/silent)
							;
                                                        sxaddpar,objhdr,'COMMENT','  '+pre+' '+systime(0)
                                ; CWI FLEX ADDITIONS/CHANGES +++
                                                        if ppar.biasskip1 then $
                                                           ecwi_apply_geom,obj,objhdr,egeom,ppar,ocub,ochdr,flex=flexpar else $
                                                              ecwi_apply_geom,obj,objhdr,egeom,ppar,ocub,ochdr
                                ; CWI FLEX ADDITIONS/CHANGES ---
							;
							; write out obj cube
							ofil = ecwi_get_imname(ppar,imgnum[i],'_ocube',/nodir)
							ecwi_write_image,ocub,ochdr,ofil,ppar
						endif
                                ;
                                ; CWI FLEX ADDITION +++
                                                if ppar.biasskip1 then $
                                                   cwi_flex_inject_two,ppar,egeom,flexpar, flexparmask
                                ; CWI FLEX ADDITION ---
                                ;
					; end if geometry is good
					endif else $
						ecwi_print_info,ppar,pre,'unusable geom for: '+obfil+' type: '+ecfg.imgtype,/error
				;
				; end check cnums and anums links
				endif else $
					ecwi_print_info,ppar,pre,'missing calibration file(s) for: '+obfil,/warning
			;
			; end check if output file exists already
			endif else begin
				ecwi_print_info,ppar,pre,'file not processed: '+obfil+' type: '+ecfg.imgtype,/warning
				if ppar.clobber eq 0 and file_test(ofil) then $
					ecwi_print_info,ppar,pre,'processed file exists already',/warning
			endelse
		;
		; end check if input file exists
		endif else $
			ecwi_print_info,ppar,pre,'input file not found: '+obfil,/error
	endfor	; loop over images
        ; CWI FLEX ADDITION +++
         if ppar.biasskip1 then $
           cwi_flex_inject_three,ppar,egeom,imgnum
	;
        ; CWI FLEX ADDITION ---
	;
	; report
	eltime = systime(1) - startime
	print,''
	printf,ll,''
	ecwi_print_info,ppar,pre,'run time in seconds',eltime
	ecwi_print_info,ppar,pre,'finished on '+systime(0)
	;
	; close log file
	free_lun,ll
	;
	return
end
